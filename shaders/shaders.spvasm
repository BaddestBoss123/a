OpCapability Shader
%glsl = OpExtInstImport "GLSL.std.450"
OpMemoryModel Logical Simple

OpEntryPoint Vertex %blit_vert "blit_vert" %gl_VertexIndex %vTexCoord %gl_PerVertex
OpEntryPoint Vertex %skybox_vert "skybox_vert" %gl_VertexIndex %vDir %gl_PerVertex
OpEntryPoint Vertex %portal_vert "portal_vert" %gl_VertexIndex %gl_PerVertex
OpEntryPoint Fragment %empty_frag "empty_frag"
OpExecutionMode %empty_frag OriginUpperLeft

OpDecorate %gl_VertexIndex BuiltIn VertexIndex
OpDecorate %vTexCoord Location 0
OpDecorate %vDir Location 0

OpMemberDecorate %PerVertex 0 BuiltIn Position
; OpMemberDecorate %PerVertex 1 BuiltIn PointSize
; OpMemberDecorate %PerVertex 2 BuiltIn ClipDistance
; OpMemberDecorate %PerVertex 3 BuiltIn CullDistance
OpDecorate %PerVertex Block

OpDecorate %PushConstants Block
OpMemberDecorate %PushConstants 0 ColMajor
OpMemberDecorate %PushConstants 0 Offset 0
OpMemberDecorate %PushConstants 0 MatrixStride 16

%Void = OpTypeVoid
%FnVoid = OpTypeFunction %Void

%F32 = OpTypeFloat 32
%U32 = OpTypeInt 32 0
%I32 = OpTypeInt 32 1
%Vec2 = OpTypeVector %F32 2
%Vec3 = OpTypeVector %F32 3
%Vec4 = OpTypeVector %F32 4
%Mat4 = OpTypeMatrix %Vec4 4

%I32_0 = OpConstant %I32 0
%I32_1 = OpConstant %I32 1
%I32_2 = OpConstant %I32 2
%I32_6 = OpConstant %I32 6
%F32_n1 = OpConstant %F32 -1
%F32_0 = OpConstant %F32 0
%F32_1 = OpConstant %F32 1
%F32_2 = OpConstant %F32 2

%43 = OpConstantComposite %Vec2 %F32_1 %F32_1

%ArrF32_1 = OpTypeArray %F32 %I32_1

%PerVertex = OpTypeStruct %Vec4
%PushConstants = OpTypeStruct %Mat4

%PtrFunctionVec2 = OpTypePointer Function %Vec2

%PtrPushConstantPushConstants = OpTypePointer PushConstant %PushConstants
%PtrPushConstantMat4 = OpTypePointer PushConstant %Mat4

%PtrInputI32 = OpTypePointer Input %I32

%PtrOutputPerVertex = OpTypePointer Output %PerVertex
%PtrOutputF32 = OpTypePointer Output %F32
%PtrOutputVec2 = OpTypePointer Output %Vec2
%PtrOutputVec3 = OpTypePointer Output %Vec3
%PtrOutputVec4 = OpTypePointer Output %Vec4

%gl_PerVertex = OpVariable %PtrOutputPerVertex Output
%gl_VertexIndex = OpVariable %PtrInputI32 Input
%pushConstants = OpVariable %PtrPushConstantPushConstants PushConstant
%vDir = OpVariable %PtrOutputVec3 Output
%vTexCoord = OpVariable %PtrOutputVec2 Output

%blit_vert = OpFunction %Void None %FnVoid
	%5 = OpLabel
	%13 = OpLoad %I32 %gl_VertexIndex
	%15 = OpShiftLeftLogical %I32 %13 %I32_1
	%17 = OpBitwiseAnd %I32 %15 %I32_2
	%18 = OpConvertSToF %F32 %17
	%20 = OpBitwiseAnd %I32 %13 %I32_2
	%21 = OpConvertSToF %F32 %20
	%22 = OpCompositeConstruct %Vec2 %18 %21
	OpStore %vTexCoord %22
	%31 = OpLoad %Vec2 %vTexCoord
	%33 = OpVectorTimesScalar %Vec2 %31 %F32_2
	%36 = OpFSub %Vec2 %33 %43
	%38 = OpCompositeExtract %F32 %36 0
 	%39 = OpCompositeExtract %F32 %36 1
	%40 = OpCompositeConstruct %Vec4 %38 %39 %F32_0 %F32_1
	%42 = OpAccessChain %PtrOutputVec4 %gl_PerVertex %I32_0
	OpStore %42 %40
	OpReturn
OpFunctionEnd

%skybox_vert = OpFunction %Void None %FnVoid
	%50 = OpLabel
	%51 = OpLoad %I32 %gl_VertexIndex
	%52 = OpShiftLeftLogical %I32 %51 %I32_1
	%53 = OpBitwiseAnd %I32 %52 %I32_2
	%54 = OpConvertSToF %F32 %53
	%55 = OpExtInst %F32 %glsl Fma %54 %F32_2 %F32_n1
	%56 = OpBitwiseAnd %I32 %51 %I32_2
	%57 = OpConvertSToF %F32 %56
	%58 = OpExtInst %F32 %glsl Fma %57 %F32_2 %F32_n1
	%59 = OpCompositeConstruct %Vec4 %55 %58 %F32_0 %F32_1
	%60 = OpAccessChain %PtrOutputVec4 %gl_PerVertex %I32_0
	OpStore %60 %59
	%61 = OpAccessChain %PtrPushConstantMat4 %pushConstants %I32_0
	%62 = OpLoad %Mat4 %61
	%63 = OpExtInst %Mat4 %glsl MatrixInverse %62
	%64 = OpLoad %Vec4 %60
	%65 = OpMatrixTimesVector %Vec4 %63 %64
	%66 = OpVectorShuffle %Vec3 %65 %65 0 1 2
	OpStore %vDir %66
	OpReturn
OpFunctionEnd

%portal_vert = OpFunction %Void None %FnVoid
	%100 = OpLabel
	%101 = OpLoad %I32 %gl_VertexIndex
	%102 = OpSMod %I32 %101 %I32_2
	%103 = OpConvertSToF %F32 %102
	%104 = OpExtInst %F32 %glsl Fma %F32_2 %103 %F32_n1
	%105 = OpSDiv %I32 %101 %I32_2
	%106 = OpConvertSToF %F32 %105
	%107 = OpExtInst %F32 %glsl Fma %F32_2 %106 %F32_n1
	%108 = OpAccessChain %PtrPushConstantMat4 %pushConstants %I32_0
	%109 = OpLoad %Mat4 %108
	%110 = OpCompositeConstruct %Vec4 %104 %107 %F32_0 %F32_1
	%111 = OpMatrixTimesVector %Vec4 %109 %110
	%112 = OpAccessChain %PtrOutputVec4 %gl_PerVertex %I32_0
	OpStore %112 %111
	OpReturn
OpFunctionEnd

%empty_frag = OpFunction %Void None %FnVoid
	%509 = OpLabel
	OpReturn
OpFunctionEnd
